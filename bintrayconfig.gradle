if (!project.rootProject.file('local.properties').exists()) {
    println "Not applying install.gradle"
    return
}
apply plugin: 'com.novoda.bintray-release'

def getBintrayUserAndKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return [
            properties.getProperty("bintray.user"),
            properties.getProperty("bintray.apikey")
    ]
}

if (shard == null) {
    throw new IllegalStateException("Must specify a shard in module's build.gradle")
} else if (libVersion == null) {
    throw new IllegalStateException("Must specify a libVersion in module's build.gradle")
} else if (libDescription == null) {
    throw new IllegalStateException("Must specify a libDescription in module's build.gradle")
}

task checkBintrayConfig {
    doLast {
        def (user, key) = getBintrayUserAndKey()
        if (user == null || user.isEmpty() ||
                key == null || key.isEmpty()) {
            throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
        }
    }
}

afterEvaluate {
    bintrayUpload.dependsOn checkBintrayConfig
}

def (user, key) = getBintrayUserAndKey()
publish {
    bintrayUser = user
    bintrayKey = key
    userOrg = 'pierfrancescosoffritti'
    groupId = 'com.pierfrancescosoffritti.androidyoutubeplayer'
    uploadName = "android-youtube-player:$shard"
    artifactId = shard
    publishVersion = libVersion
    desc = libDescription
    website = 'https://pierfrancescosoffritti.github.io/android-youtube-player'
    dryRun = false
}